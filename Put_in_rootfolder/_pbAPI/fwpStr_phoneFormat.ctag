<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpStr_phoneFormat.ctag }
	{rsrcType=		tag }
	{rsrcName=		fwpStr_phoneFormat }
	{rsrcHTTP=		http://www.pageblocks.org/refc/fwpStr_phoneFormat }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }
	{authorHTTP=	www.pageblocks.org }

	{desc=			formats a string of digits into various phone number formats }

	{maintvsrn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//............................................................................
//
//	packed format should be 0001112222333333
//	where:	000 = area code
//			111 = prefix
//			2222 = number
//			333333 = extension
//	where there is no area code, the data should be padded with 000
//	there is no need to pad the extension
//.............................................................................

define_tag:'fwpStr_phoneFormat', -priority='replace',
	-required='phone',
	-optional='format';

	local:
		'fw_phRaw' = @local:'phone',
		'fw_phNmbr' = (string),
		'fw_format' = @local:'format';

	if: #fw_phRaw;
		if: #fw_format == '';
			#fw_format = '.';
		/if;
	
		if: #fw_format == '(';
	
			if: #fw_phRaw->(substring:1,3) != '000';
				#fw_phNmbr = '(' + (#fw_phRaw->(substring:1,3)) + ') ';
			/if;
			#fw_phNmbr += 
				(#fw_phRaw->(substring:4,3)) + '-' +
				(#fw_phRaw->(substring:7,4));
	
		else: #fw_format == '/';
	
			if: (#fw_phRaw->(substring:1,3)) != '000';
				#fw_phNmbr = (#fw_phRaw->(substring:1,3)) + '/';
			/if;
			#fw_phNmbr += 
				(#fw_phRaw->(substring:4,3)) + '-' +
				(#fw_phRaw->(substring:7,4));
		else;
	
			if: (#fw_phRaw->(substring:1,3)) != '000';
				#fw_phNmbr = (#fw_phRaw->(substring:1,3)) + #fw_format;
			/if;
			#fw_phNmbr += 
				(#fw_phRaw->(substring:4,3)) + #fw_format +
				(#fw_phRaw->(substring:7,4));
		/if;
	
		if: #fw_phRaw->size > 10;
			#fw_phNmbr += ' x' + (#fw_phRaw->(substring:11));
		/if;
	/if;
	
	return: #fw_phNmbr;

/define_tag;
?>
<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpPage_requestURLElements.ctype }
	{rsrcType=		type }
	{rsrcName=		fwp_requestURLElements }
	{rsrcHTTP=		www.pageblocks.org/refc/fwp_requestURLElements }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }
	{authorHTTP=	www.pageblocks.org }

	{desc=			Creates instance vars of various URL components. Also 
					determines if params have been embedded in the URL
					as created by fwpPage_makeURLparams.ctag and turns them
					in to vars and rewrite the real `file` name. }

	{maintvsrn=		1.3.0 }
	{maintrelease=	5.3.0 }
	{maintdate=		2007-08-02 }
	{maintauthor=	Greg Willits }
	{maintnotes=	added ->endsWithSlash tag which was supposed to be there
	 				all along }

	{maintvsrn=		1.2.0 }
	{maintrelease=	5.1.0 }
	{maintdate=		2006-08-21 }
	{maintauthor=	Greg Willits }
	{maintnotes=	changed name from fwp_URLPaths as part of the all-new
					url rewriter capabilities, but everything else is 
					identical }

	{maintvsrn=		1.1.0 }
	{maintrelease=	5.1.0 }
	{maintdate=		2006-08-21 }
	{maintauthor=	Greg Willits }
	{maintnotes=	added ->page as a synonym for file }

	{maintvsrn=		1.0.1 }
	{maintrelease=	5.0.2 }
	{maintdate=		2006-04-11 }
	{maintauthor=	Greg Willits }
	{maintnotes=	changed calculation of #path to fix a bug
					rearranged some var declarations to make it easier to
					deprecate older var names }

	{maintvsrn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//.............................................................................

define_type:'fwp_requestURLElements', -prototype;

	local:
		'host'			= string,
		'subhost'		= string,
		'domain'		= string,
		'subdomain'		= string,
		'url'			= string,
        'elements'		= array,
        'oddElements'	= array,
        'evenElements'	= array;

define_tag:'onCreate';

	(self->'url') 		= response_filepath;
	
	local:'urlTrimmed' = self->'url';
	#urlTrimmed->removeLeading:'/';
	#urlTrimmed->removeTrailing:'/';

	(self->'elements')	= #urlTrimmed->split:'/';

	(self->'host') = (string_findregexp: client_headers, -find='HOST:\\s?(.*)', -ignorecase)->last;
	(self->'subHost') = (self->'host')->(split:'.')->get:1;
	(self->'host')->trim;
	(self->'subhost')->trim;
	(self->'domain') = (self->'host');
	(self->'domain')->removeleading:((self->'subhost') + '.');
	(self->'subdomain')	= @(self->'subhost');

/define_tag;

//-------------------------------------

define_tag:'extractOddEvenElements';

	local:'thisOne' = string;
	iterate: self->'elements', #thisOne;
		loop_count%2
			? (self->'evenElements')->insert:#thisOne
			| (self->'oddElements')->insert:#thisOne;
	/iterate;

/define_tag;

//-------------------------------------

define_tag:'endsWithSlash';

	(self->'url')->endsWith:'/'
		? return: true
		| return: false;

/define_tag;

/define_type;
?>

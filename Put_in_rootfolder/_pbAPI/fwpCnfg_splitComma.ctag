<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpCnfg_splitComma.ctag }
	{rsrcType=		tag }
	{rsrcName=		fwpCnfg_splitComma }
	{rsrcHTTP=		www.pageblocks.org/refc/fwpCnfg_splitComma }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }
	{authorHTTP=	www.pageblocks.org }

	{desc=			This routine converts a comma list into an array, or 
					using the -nosplit option simply eliminates all white
					space surrounding the commas. }

	{maintvsrn=		1.2.0 }
	{maintrelease=	5.2.0 }
	{maintdate=		2007-06-09}
	{maintauthor=	Greg Willits }
	{maintnotes=	updated debug and error handling systems,
	 				changed some internal vars for better readability }

	{maintvsrn=		1.1.0 }
	{maintrelease=	5.1.0 }
	{maintdate=		2006-05-30}
	{maintauthor=	Greg Willits }
	{maintnotes=	added tagTrace }

	{maintvsrn=		1.0.2 }
	{maintrelease=	5.0.0 b5 }
	{maintdate=		2006-02-02 }
	{maintauthor=	Greg Willits }
	{maintnotes=	doh! fixed type detection logic which didn't allow
					for empty input }

	{maintvsrn=		1.0.1 }
	{maintrelease=	5.0.0 b5 }
	{maintdate=		2006-01-27 }
	{maintauthor=	Greg Willits }
	{maintnotes=	improved white space detection and removal, changed
					some syntax for efficiency }

	{maintvsrn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//.............................................................................

define_tag:'fwpCnfg_splitComma', -priority='replace',
	-required='list';

//	-withoutSplit (-nosplit deprecated))

	($fw_debug >= fw_kVerbose) ? $fw_tagTracer->(add:'fwpCnfg_splitComma', -list=(string:#list)->(getLeft:80));

	local:
		'configData'	= local:'list',
		'withoutSplit' 	= false;

	(params->find:'-withoutSplit') || (params->find:'-nosplit')
		? #withoutSplit = true;

	if: #configData; 
		if:	((#configData->type != 'string') && (#configData->type != 'bytes'));
			$fw_error->(insert:'5201'='fwpCnfg_splitComma');
			$fw_debug ? $fw_apiError->(insert:'5242'=(#list->type));
			$fw_debug ? $fw_tagTracer->(add:'fwpCnfg_splitComma', -ERROR = 'input was not a string or bytes type');
			$fw_criticalLog ? log_critical:'pbError : input to fwpCnfg_splitComma was not a string or bytes type';
		else;
	
			 #configData = (string_replaceRegExp:
				#configData,
				-find = '\\s*,\\s*',
				-replace = ',');
	
			!#withoutSplit
				? #configData = (#configData->split:',');
		
			return: #configData;
		/if;
	else;
		($fw_debug >= fw_kVerbose) ? $fw_tagTracer->(add:'fwpCnfg_splitComma', -status = 'input was empty');
	/if;

/define_tag;
?>
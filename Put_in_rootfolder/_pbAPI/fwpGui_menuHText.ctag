<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpGui_menuHText.ctag }
	{rsrcType=		tag }
	{rsrcName=		fwpGui_htextmenu }
	{rsrcHTTP=		www.pageblocks.org/refc/fwpGui_htextmenu }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }

	{desc=			Creates a horizontal text menu from either
					a structured config file (mnuText_), or an array which
					is structured identical to the config file format. }

	{maintvsrn=		1.3 }
	{maintrelease=	5.3.0 }
	{maintdate=		2007-08-09 }
	{maintauthor=	Greg Willits }
	{maintnotes=	added fwptxtmenuhilitelink class }

	{maintvsrn=		1.2 }
	{maintrelease=	5.2.0 }
	{maintdate=		2007-06-09 }
	{maintauthor=	Greg Willits }
	{maintnotes=	added process to menuItemTitle to allow for MVS,
	 				changed internal vars names for better readability }

	{maintvsrn=		1.1 }
	{maintrelease=	5.1.0 }
	{maintdate=		2006-05-26 }
	{maintauthor=	Greg Willits }
	{maintnotes=	converted timers to fwp_timer ctype }

	{maintvrsn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//............................................................................

define_tag:'fwpGui_htextmenu', -priority='replace',
	-optional='menu',
	-optional='tween',
	-optional='left',
	-optional='right',
	-optional='paramsGet',
	-optional='paramsFile',
	-optional='paramsFldrs';

	$fw_debugTimers ? $fw_timer->(start:'menuTagHtext');

	local:
		'menuConfigSource'	= @local:'menu',
		'menuFill'			= @local:'tween',
		'menuBorderLeft'	= @local:'left',
		'menuBorderRight'	= @local:'right',
		'paramsViaGet'		= local:'paramsGet',
		'paramsViaFile'		= local:'paramsFile',
		'paramsViaFldrs'	= local:'paramsFldrs',
		'linkPath'			= string,
		'linkFile'			= string,
		'thisParam'			= string,
		'menuConfigData'	= string,
		'menuConfigLine'	= string,
		'menuMarkup'		= string,
		'menuItemIndx'		= 0,
		'currentPage' 		= ((response_filepath)->split:'/')->last,
		'currentPath' 		= (response_filepath) - #currentPage;

	if: #paramsViaGet;
		#paramsViaGet = (fwpCnfg_splitComma: #paramsViaGet);
	/if;

	if: #currentPage >> fw_kURLparamsChar;
		local:'currentPage'=((response_filepath)->split: fw_kURLparamsChar)->last;
	/if;

	if: #currentPath >> '/-/';
		#currentPath = #currentPath->split:'/-/'->get:1;
		#currentPath += '/';
	/if;


//	if menuConfigSource is a string then it`s a file name to be loaded
//	if menuConfigSource is already an array then we have direct config info

	if: (#menuConfigSource->type == 'string') || (#menuConfigSource->type == 'bytes');

		#menuConfigData = (fwpCnfg_loadLines: ('mnuText_' + #menuConfigSource + fw_kCnfgExt));

	else: #menuConfigSource->type == 'array';

		#menuConfigData = #menuConfigSource;

	/if;


//	start building the menu

	(#menuMarkup) += (#menuBorderLeft);

	iterate: #menuConfigData, #menuConfigLine;

		local:'menuConfigItems' = #menuConfigLine->split:'```';
		local:
			'menuItemLink'		= #menuConfigItems->get:1,
			'menuItemTitle'		= #menuConfigItems->get:2,
			'menuItemMatchBy'	= #menuConfigItems->get:(#menuConfigItems->size);
		local:'menuURLItems'	= (#menuItemLink->(split:'/'));
		local:'menuLinkPage'	= (#menuURLItems->last);
		local:'menuLinkPath'	= (#menuItemLink);
		#menuLinkPath->(removeTrailing:#menuLinkPage);

		if: #paramsViaGet->type == 'array';

			(#menuItemLink) += '?';
			iterate: #paramsViaGet, #thisParam;
				if: #thisParam >> '=';
					(#menuItemLink) += ((#thisParam->split:'=')->get:1) + '=' + ((#thisParam->split:'=')->get:2) + '&amp;';
				else;
					(#menuItemLink) += #thisParam + '=' + (var:#thisParam) + '&amp;';
				/if;
			/iterate;
			#menuItemLink->(removeTrailing:'&amp;');

		else: #paramsViaFile;

			#linkFile = (#menuItemLink->split:'/')->last;
			#linkPath = #menuItemLink - #linkFile;
			(#menuItemLink) = (fwpPage_urlParamsFile: -folder=#linkPath, -page=#linkFile, -vars=#paramsViaFile);

		else: #paramsViaFldrs;

			#linkFile = (#menuItemLink->split:'/')->last;
			#linkPath = #menuItemLink - #linkFile;
			(#menuItemLink) = (fwpPage_urlParamsFldrs: -folder=#linkPath, -page=#linkFile, -vars=#paramsViaFldrs);

		/if;

		if: 
			((#menuItemMatchBy == 'path') && (#currentPath->beginswith:#menuLinkPath)) ||
			((#menuItemMatchBy == 'page') && (#currentPage == #menuLinkPage));

			#menuMarkup += '<span class="fwptxtmenuhilite"><a class="fwptxtmenuhilitelink" href="';
			#menuMarkup += #menuItemLink;
			#menuMarkup += '">';
			#menuItemTitle->beginsWith:'['
				? #menuMarkup += (process:#menuItemTitle)
				| #menuMarkup += #menuItemTitle;
			#menuMarkup += '</a></span>';
			#menuMarkup += #menuFill;

		else;
			#menuMarkup += '<a href="';
			#menuMarkup += #menuItemLink;
			#menuMarkup += '">';
			#menuItemTitle->beginsWith:'['
				? #menuMarkup += (process:#menuItemTitle)
				| #menuMarkup += #menuItemTitle;
			#menuMarkup += '</a>';
			#menuMarkup += #menuFill;
		/if;

	/iterate;

	#menuMarkup->(removeTrailing:#menuFill);

	(#menuMarkup) += (#menuBorderRight);

	$fw_debugTimers ? $fw_timer->(stop:'menuTagHtext');

	return: @#menuMarkup;

/define_tag;
?>
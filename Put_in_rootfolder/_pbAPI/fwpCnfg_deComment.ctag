<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpCnfg_deComment.ctag }
	{rsrcType=		tag }
	{rsrcName=		fwpCnfg_deComment }
	{rsrcHTTP=		www.pageblocks.org/refc/fwpCnfg_deComment }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }
	{authorHTTP=	www.pageblocks.org }

	{desc=			takes an array of items (typically retrieved from a config
					data file and removes items deemed non data }

	{maintvsrn=		1.2.0 }
	{maintrelease=	5.2.0 }
	{maintdate=		2007-06-09}
	{maintauthor=	Greg Willits }
	{maintnotes=	updated debug and error handling systems,
	 				changed some internal vars for better readability }

	{maintvsrn=		1.1.0 }
	{maintrelease=	5.1.0 }
	{maintdate=		2006-05-30}
	{maintauthor=	Greg Willits }
	{maintnotes=	added tagTrace }

	{maintvsrn=		1.0.1 }
	{maintrelease=	5.0.0 b5 }
	{maintdate=		2006-02-02 }
	{maintauthor=	Greg Willits }
	{maintnotes=	doh! fixed type detection logic which didn't allow
					for empty input }

	{maintvsrn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//.............................................................................

define_tag:'fwpCnfg_deComment', -priority='replace',
	-required = 'cnfgData';

	($fw_debug >= fw_kVerbose)	? $fw_tagTracer->(add:'fwpCnfg_deComment', -rows = #cnfgData->size);

	local: 
		'configData'	= #cnfgData,
		'cnfgLine'		= string;

	//	remove all comments and empty lines
	//	must use loop because we're counting backwards to delete lines

	if: #configData;
		if: (#configData->type) != 'array';
			$fw_error->(insert:'5201'='fwpCnfg_deComment');
			$fw_debug ? $fw_apiError->(insert:'5240'=(#cnfgData->type));
			$fw_debug ? $fw_tagTracer->(add:'fwpCnfg_deComment', -ERROR = 'input was not an array');
			$fw_criticalLog ? log_critical:'pbError : input to fwpCnfg_deComment was not an array';
			return: array;
		else;
			loop: -from=#configData->size, -to=1, -by=(-1);
				#cnfgLine = #configData->(get:loop_count);
				if: (#cnfgLine == '') 
					|| ((string_findRegExp: #cnfgLine, -find='\\S+')->size == 0) 
					|| (#cnfgLine >> 'output_none') 
					|| (#cnfgLine->(beginsWith:'#')) 
					|| (#cnfgLine->(beginsWith:'//'));
		
					#configData->(remove:loop_count);
				/if;
			/loop;
			return: #configData;
		/if;
	/if;	
/define_tag;
?>

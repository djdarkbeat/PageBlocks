<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpCnfg_loadVars.ctag }
	{rsrcType=		tag }
	{rsrcName=		fwpCnfg_loadVars }
	{rsrcHTTP=		www.pageblocks.org/refc/fwpCnfg_loadVars }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }
	{authorHTTP=	www.pageblocks.org }

	{desc=			This tag loads a configuration file,
					removes comment lines, and 
					splits the lined into an array,
					takes each array pair and makes variables }

	{maintvsrn=		1.3.0 }
	{maintrelease=	5.2.0 }
	{maintdate=		2007-06-09}
	{maintauthor=	Greg Willits }
	{maintnotes=	updated debug and error handling systems,
	 				changed some internal vars for better readability }

	{maintvsrn=		1.2.0 }
	{maintrelease=	5.1.0 }
	{maintdate=		2006-05-30}
	{maintauthor=	Greg Willits }
	{maintnotes=	added tagTrace }

	{maintvsrn=		1.1 }
	{maintrelease=	5.0.0 b5 }
	{maintdate=		2006-01-27 }
	{maintauthor=	Greg Willits }
	{maintnotes=	changed some styntax for efficiency,
					added -despace option,
					eliminated code used by old RAMbucket caching }

	{maintvsrn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//.............................................................................

define_tag:'fwpCnfg_loadVars', -priority='replace',
	-required='file',
	-optional='username',
	-optional='password';

//	-makeLocals (-locals deprecated)
//	-removeWhiteSpace (-despace deprecated)
//	-withoutCaching (-nocache deprecated)

	($fw_debug >= fw_kChatty) ? $fw_tagTracer->(add:'fwpCnfg_loadVars', -file = #file);

	local:
		'fileName' 			= @#file,
		'withoutCaching' 	= false,
		'removeWhiteSpace' 	= false,
		'makeLocals' 		= false,
		'configData' 		= string,
		'configDataStr'		= string;

//	pull from cache if available
//	else pull from disk (and store to cache)

	#configData = $fw_gConfigCache->(restore:#fileName);

	if: #configData->size > 0;

		// pulled from cache, so nothing to do

	else;

//	load file data

		(params->find:'-makeLocals') || (params->find:'-asLocals') || (params->find:'-intoLocals') || (params->find:'-locals')
			? #makeLocals = true;

		(params->find:'-withoutCaching') || (params->find:'-nocache')
			? #withoutCaching = true;

		(params->find:'-removeWhiteSpace') || (params->find:'-despace')
			? #removeWhiteSpace = true;

		#configData = (fwpCnfg_loadFile:
			-file		= #fileName,
			-username 	= local:'username',
			-password 	= local:'password');

//	despace the delimiters

		if: #removeWhiteSpace;
			#configData = (string_replaceRegExp: 
				#configData, 
				-find = '\\s*===\\s*',
				-replace = '===');
			#configData = (string_replaceRegExp: 
				#configData, 
				-find = '\\s*=\\s*',
				-replace = '=');
		/if;

//	convert to array so we can remove comments

		#configData = (fwpCnfg_splitLines: #configData);
		#configData = (fwpCnfg_deComment: #configData);

//	cache or not?
//	cached as an array of source still be turned into vars

		!#withoutCaching
			? $fw_gConfigCache->(add:
				-name  = #fileName,
				-value = #configDataStr);
	/if;

//	make variables or locals from the pairs

	if: #makeLocals;
		return: (fwpCnfg_makeVars: #configData, -makeLocals);
	else;
		return: (fwpCnfg_makeVars: #configData);
	/if;

/define_tag;
?>
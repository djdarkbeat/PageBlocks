<?lassoscript
//............................................................................
//
//	pageblocks: (c) 2002-2007 http://www.pageblocks.org/
//
//............................................................................
/*

	{fileName=		fwpLog_err.ctag }
	{rsrcType=		tag }
	{rsrcName=		fwpLog_err }
	{rsrcHTTP=		www.pageblocks.org/refc/fwpLog_err }

	{lassoVrsnMin=	8.1.0 }
	{lassoVrsnMax=	8.5.3 }

	{author=		Greg Willits }
	{authorEmail=	subscribe to pbTalk at www.pageblocks.org/talk/ }
	{authorHTTP=	www.pageblocks.org }

	{desc=			Creates a log of FWPro and Lasso errors. }

	
	{maintvsrn=		1.1.1 }
	{maintrelease=	5.2.4 }
	{maintdate=		2007-07-21 }
	{maintauthor=	Greg Willits }
	{maintnotes=	added tagTacer }

	{maintvsrn=		1.1 }
	{maintrelease=	5.2.0 }
	{maintdate=		2007-06-09 }
	{maintauthor=	Greg Willits }
	{maintnotes=	added log_critical }

	{maintvsrn=		1.0.1 }
	{maintrelease=	5.0.0 b5 }
	{maintdate=		2006-02-02 }
	{maintauthor=	Greg Willits }
	{maintnotes=	changed db/tbl references to use new vars }

	{maintvsrn=		1.0 }
	{maintrelease=	5.0.0 }
	{maintdate=		2006-01-16 }
	{maintauthor=	Greg Willits }
	{maintnotes=	initial release }

*/
//.............................................................................

define_tag:'fwpLog_err', 
	-autooutput,
	-priority='replace',
	-optional='id',
	-optional='code',
	-optional='errorMsg',
	-optional='pageInfo',
	-optional='fwpInfo';

	$fw_debug >= fw_kChatty ? $fw_tagTracer->(add:'fwpLog_err', -errorMsg=local:'errorMsg');
	
	local:'fw_logFileNm'=($fw_sPath->'logs') + 'errlog_' + (date->year);

	//	backwards compatibility
	//	doesn`t require the declaration of fw_gLogErrTarget

	if: !(global_defined:'fw_gLogErrTarget');
		$fw_gLogErrTarget = 'file';
	/if;

	if: $fw_gLogErrTarget == 'file';

		inline:
			-username=$fw_gFilesUser,
			-password=$fw_gFilesPswd;
		
			select: $fw_gLogErrRoll;
				case:'M';
					#fw_logFileNm += '_m' + (date->month);
				case:'W';
					#fw_logFileNm += '_w' + (date->week);
				case:'D';
					#fw_logFileNm += '_d' + (date->dayofyear);
			/select;
			#fw_logFileNm += '.txt';
		
			log: #fw_logFileNm;
				#id;
				'\t';
				#code;	
				'\t';
				server_date: -extended;	
				'\t';
				server_time;	
				'\t';
				client_IP;	
				'\t';
				response_filepath;	
				'\t';
				referrer_URL;	
				'\t';
				#errorMsg;
				'\t';
				#pageInfo;
				'\t';
				#fwpInfo;
				'\n';
			/log;

			$fw_criticalLog && ((file_currentError: -errorcode)  != 0)
				? log_critical:('pbError : fwpLog_err -- ' + file_currentError);
		
		/inline;

	else: $fw_gLogErrTarget == 'database';
	
		local:'logPairs' = (array:
			'logErrID' 		= #id,
			'logErrCode' 	= #code,
			'logDate' 		= server_date: -extended,
			'logTime' 		= server_time,
			'logClientIP' 	= client_IP,
			'logPageURL' 	= response_filepath,
			'logPageRef' 	= referrer_URL,
			'logErrMsg' 	= #errorMsg, 
			'logPageInfo' 	= #pageInfo,
			'logFWPInfo' 	= #fwpInfo);

		fwpLog_asyncSQL:
			-logPairs	= #logPairs,
			-user		= $fw_gQueryUser,
			-pswd		= $fw_gQueryPswd,
			-db			= $fw_gDatabases->find:'logs',
			-tbl		= $fw_gTables->find:'logerr',
			-keyfld		= 'id';

	/if;
/define_tag;
?>
